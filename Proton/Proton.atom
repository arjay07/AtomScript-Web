#    _____           _              
#   |  __ \         | |             
#   | |__) | __ ___ | |_ ___  _ __  
#   |  ___/ '__/ _ \| __/ _ \| '_ \ 
#   | |   | | | (_) | || (_) | | | |
#   |_|   |_|  \___/ \__\___/|_| |_| v0.2
#                                   
#	Â©ZeroSeven Interactive 2015   
#	API for AtomScript
# 

@Proton = {};
@Doc = document;
@Window = window;

#Stats
Proton<stats> = {

	currZIndex: 999

};

#List
Proton<list> = $(){
	
	this -> length = 0;
	
	if(arguments::length > 0){
	
		for(@i = 0; i < arguments::length; i++){
	
			this [i] = arguments[i];
			this -> length++;

		}
	
	}
	
	this -> add = $(obj){
	
		@i = this -> length;
		@i2 = i++;
		this [i2] = obj;
		this -> length++;
	
	}
	
	this -> set = $(index, obj){
	
		this [index] = obj;
	
	}
	
	this -> indexOf = $(obj, pos){
	
		pos = 0;
	
		for(@i = 0; i < this -> length; i++){
			
			if(this [pos + i] == obj){
			
				return pos + i;
				break;
			
			}
			
		}
		
		return -1;
		
	}
	
	this -> clear = $(){
	
		for(@i = 0; i < this -> length; i++){
		
			delete this[i];
		
		}
		
		this -> length = 0;
	
	}
	
	this -> toArray = $(array){
	
		@a = [];
	
		for(@i = 0; i < this -> length; i++){
		
			a::push(this[i]);
		
		}
		
		if(array!=null){
		
			array = a;
		
		}
		
		return a;
	
	}

}

#Pointer
Proton<Pointer> = {x: 0, y: 0, state: "up"};

Proton<Pointer>update = $(e){

	if(e.pageX || e.pageY){
	
		Proton<Pointer>x = e.pageX;
		Proton<Pointer>y = e.pageY;
	
	}else if(e.clientX || e.clientY){
	
		Proton<Pointer>x = e.clientX + document.documentElement.scrollLeft;
		Proton<Pointer>y = e.clientY + document.documentElement.scrollTop;
	
	}

};

Doc<addEventListener>("mousemove", Proton::Pointer<update>);
Doc<addEventListener>("mousedown", $(){

	Proton<Pointer>state = "down";

});
Doc<addEventListener>("mouseup", $(){

	Proton<Pointer>state = "up";

});

#DOM Methods
Proton<createButton> = $(text){

	@i = Doc<createElement>("button");
	i<innerHTML> = text;
	i<onClick> = $(onclick){
	
		i<addEventListener>("click", onclick);
	
	};
	
	i<setText> = $(t){
	
		i<innerHTML> = t;
	
	}
	
	i<getText> = $(){
	
		return i<innerHTML>;
		
	}
	
	return i;

}

Proton<createTextInput> = $(){

	@i = document<createElement>("input");
	i<type> = "text";
	i<onKeyPress> = $(onpress){
	
		i<addEventListener>("keydown", onpress);
	
	};
	
	i<setText> = $(t){
	
		i<value> = t;
	
	}
	
	i<getText> = $(){
	
		return i<value>;
		
	}
	
	return i;

}

Proton<createText> = $(text){

	@i = document<createElement>("span");
	i<innerText> = text;
	
	i<setText> = $(t){
		
		i.innerHTML = t;
	
	}
	
	i<getText> = $(){
	
		return i<innerText>;
	
	}
	
	return i;

}

Proton<createDiv> = $(){

	@i = Doc<createElement>("div");
	return i;

}

Proton<append> = $(element, parent){

	if(parent==null)Doc::body<appendChild>(element);
	else parent<appendChild>(element);

}

Proton<createTable> = $(columns, rows){

	@table = Doc<createElement>("table");
	table<table> = [];
	table<size> = 0;
	table<columnCount> = columns;
	table<rowCount> = rows;
	table<cells> = [];
	
	for(@i = 0; i < rows; i++){
	
		@row = Doc<createElement>("tr");
		@rowArray = [];
		
		for(@a = 0; a < columns; a++){

			@data = Doc<createElement>("td");
			Proton<append>(data, row);
			rowArray<push>(data);
			table::cells<push>(data);
			table<size>++;

		}
		
		Proton<append>(row, table);
		table::table<push>(rowArray);
	
	}
	
	table<getCell> = $(x, y){
	
		@cell = table<table>[x][y];
		return cell;
	
	}
	
	table<applyToCells> = $(applier){
	
		for(@r = 0; r < table::rowCount; r++)
			for(@c = 0; c < table::columnCount; c++){
			
				@cell = table<getCell>(r, c);
				applier(cell);
			
			}
	
	}
	
	return table;

}

Proton<createList> = $(array, type){

	@list;

	if(type == 0 || type == "ul" || type == null){
	
		list = Doc<createElement>("ul");
	
	}else if(type == 1 || type == "ol"){
	
		list = Doc<createElement>("ol");
	
	}

	for(@i = 0; i < array::length; i++){
	
		el = array[i];
		@item = Doc<createElement>("li");
		if(el.constructor == String || el.constructor == Number){
			
			item<innerHTML> = el;
		
		}else{
			
			item<innerHTML> = el;
		
		}
		
		Proton<append>(item, list);
	
	}
	
	return list;

}

Proton<createDialog> = $(width, height, x, y, title, innerContent){

	@dialog = Proton<createDiv>();
	@titleBar = Proton<createDiv>();
	@content = Proton<createDiv>();
	@optionBar = Proton<createDiv>();
	
	dialog::style<width> = width;
	dialog::style<height> = height;
	Proton<style>(dialog, "position", "absolute");
	Proton<style>(dialog, "left", x + "px");
	Proton<style>(dialog, "top", y + "px");
	Proton<style>(dialog, "margin", 0);
	Proton<style>(dialog, "padding", 0);
	#Proton<style>(dialog, "-moz-user-select: none; -khtml-user-select: none; -webkit-user-select: none; -o-user-select: none; user-select: none;");
	
	titleBar<innerHTML> = "<h2>" + title + "</h2>";
	Proton<style>(titleBar, "cursor: pointer; position: absolute; top: 0; left: 0; width: 100%; display: block; height: 20%;-moz-user-select: none; -khtml-user-select: none; -webkit-user-select: none; -o-user-select: none; user-select: none;");
	
	content<innerHTML> = innerContent;
	Proton<style>(content, "position: relative; top: 20%; left: 0; width: 98%; height: 60%; display: block; overflow: auto;");
	
	Proton<style>(optionBar, "position: relative; top: 20%; left: 0; width: 100%; height: 20%; display: block; text-align: center;-moz-user-select: none; -khtml-user-select: none; -webkit-user-select: none; -o-user-select: none; user-select: none;");
	
	Proton<append>(titleBar, dialog);
	Proton<append>(content, dialog);
	Proton<append>(optionBar, dialog);
	
	dialog<titleBar> = titleBar;
	dialog<content> = content;
	dialog<optionBar> = optionBar;
	
	titleBar<onmousemove> = $(){
	
		if(Proton<Pointer>state == "down"){
		
			@pointerX = Proton::Pointer::x - parseInt(width::replace("px", ""))/2;
			@pointerY = Proton::Pointer::y - parseInt(height::replace("px", "")) + parseInt(height::replace("px", "")) - parseInt(titleBar::style::height::replace("px", ""));
			Proton<style>(dialog, "left", pointerX + "px");
			Proton<style>(dialog, "top", pointerY + "px");
		
		}
	
	}
	
	dialog<create> = $(parent){
	
		if(parent!=null)Proton<append>(dialog, parent);
		else Proton<append>(dialog);
		
		dialog::titleBar<addEventListener>("mousedown", $(){});
		
		dialog::style<display> = "none";
	
	}
	
	dialog<show> = $(){
	
		dialog::style<display> = "block";
		Proton<stats>currZIndex++;
		Proton<style>(dialog, "zIndex", Proton::stats::currZIndex);
	
	}
	
	dialog<close> = $(){
	
		dialog::style<display> = "none";
	
	}
	
	return dialog;

}

Proton<style> = $(element, style, value){

	if(value == null){
	
		@styleAttr = "";
		if(element<getAttribute>("style") != null)styleAttr = element<getAttribute>("style");
		element<setAttribute>("style", styleAttr + style);
	
	}else{
	
		element::style[style] = value;
	
	}

}

Proton<CSS> = $(){

	@style = Doc<createElement>("style");
	
	style<appendChild>(Doc<createTextNode>(""));
	
	Doc::head<appendChild>(style);
	
	@sheet = style<sheet>;
	
	sheet<add> = $(selector, rules, index){
	
		if("insertRule" in sheet){
		
			sheet<insertRule>(selector + "{" + rules + "}", index )
		
		}else if("addRule" in sheet){
		
			sheet<addRule>(selector, rules, index);
		
		}
	
	}
	
	return sheet;

}

Proton<get> = $(id, root){
	
	if(root == null)root = document;

	if(id<startsWith>(":")){

		return root<getElementById>(id<substring>(1, id::length));

	}else if(id<startsWith>(".")){

		return root<getElementsByClassName>(id<substring>(1, id::length));

	}else if(id<startsWith>("<") && id.endsWith(">")){ 

		return root<getElementsByTagName>(id<substring>(1, id::length-1));

	}else if(id<startsWith>("^")){

		return root<getElementsByName>(id<substring>(1, id::length));

	}else{

		return id;

	}
	
}
